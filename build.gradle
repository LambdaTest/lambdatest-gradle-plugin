plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.3.0'
    id 'com.diffplug.spotless' version '6.25.0'
}

version = '1.0.7'
group = 'io.github.lambdatest'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.24.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.24.1'
    
    // Testing dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testImplementation 'org.mockito:mockito-core:5.5.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.5.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
    testImplementation 'org.assertj:assertj-core:3.24.2'
}

test {
    useJUnitPlatform()
    jvmArgs = [
        '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
        '--add-opens', 'java.base/java.util=ALL-UNNAMED'
    ]
}

gradlePlugin {
    website = 'https://www.lambdatest.com'
    vcsUrl = 'https://github.com/LambdaTest/lambdatest-gradle-plugin'
    plugins {
        lambdatestPlugin {
            id = 'io.github.lambdatest.gradle'
            implementationClass = 'io.github.lambdatest.gradle.LambdaTestPlugin'
            displayName = 'LambdaTest Gradle Plugin'
            description = 'Runs Espresso tests on LambdaTest'
            tags.set(['espresso', 'test', 'lambdatest', 'app', 'automation', 'appautomation'])
        }
    }
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat().aosp()
        formatAnnotations()
    }
}

javadoc {
    options.tags = [ "implNote:a:Implementation Note:" ]
}
